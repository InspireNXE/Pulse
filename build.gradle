// Gradle plugins
plugins {
    id 'application'
    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'java'
    id 'maven'
    id 'signing'
}

// Project repositories
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'oss-sonatype'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
    maven {
        name = 'spongepowered-sonatype'
        url = 'http://repo.spongepowered.org/maven/'
    }
    maven {
        name = "spacehq"
        url = "https://repo.spacehq.org/content/repositories/snapshots"
    }
}

// Project dependencies
dependencies {
    compile 'org.spongepowered:spongeapi:4.1.0-SNAPSHOT'
    compile 'org.spacehq:mcprotocollib:1.9.2-SNAPSHOT'
    compile 'org.apache.logging.log4j:log4j-api:2.5'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
    compile 'net.sf.jopt-simple:jopt-simple:5.0'
    compile 'jline:jline:2.14.1'
    compile 'junit:junit:4.11'
}

// Gradle default tasks
defaultTasks 'licenseFormat', 'clean', 'build', 'shadowJar'

// Basic project information
group = 'org.inspirenxe'
archivesBaseName = 'pulse'
version = '1.0.0-SNAPSHOT'
mainClassName = 'org.inspirenxe.pulse.Main'
ext.inceptionYear = '2014'
ext.currentYear = '2015'
ext.packaging = 'jar'

// Extended project information
ext.projectName = 'Pulse'
ext.description = 'Pulse is an open-source Minecraft server written in Java that implements Sponge API'
ext.url = 'http://inspirenxe.org'
ext.organization = 'InspireNXE'

// Java version target/source compatibility
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Compilation variables
ext.buildNumber = project.hasProperty("buildNumber") ? buildNumber : '0'
ext.commit = project.hasProperty("commit") ? commit : 'unknown'

// Filter, process, and include resources
processResources {
    from(rootProject.rootDir) {
        include 'LICENSE.txt'
    }
}

// Include dependencies in the final JAR
shadowJar {
    classifier = ''
}

// License header formatting
license {
    ext.name = project.name
    ext.organization = organization
    ext.url = url
    ext.year = inceptionYear == currentYear ? inceptionYear : inceptionYear + '-' + currentYear
    header rootProject.file('HEADER.txt')
    ignoreFailures true
    strictCheck true
    includes(["**/*.java"])
}

// JAR manifest configuration
jar.manifest.mainAttributes(
        "Main-Class": mainClassName,
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": version + "-b" + buildNumber + ".git-" + commit,
        "Implementation-Vendor": url)

// Artifact deployment
uploadArchives {
    repositories.mavenDeployer {
        // Javadoc JAR generation
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from 'build/docs/javadoc'
        }

        // Source JAR generation
        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.java.srcDirs
        }

        // Set all artifacts
        artifacts {
            archives jar, javadocJar, sourcesJar
        }

        // Tasks and variables based on if release or snapshot
        if (version.endsWith('-SNAPSHOT')) {
            // Set variable to snapshots repository URL
            ext.sonatypeUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
        } else {
            // Set variable to releases repository URL
            ext.sonatypeUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

            // Deployment signing
            signing {
                // Check if uploadArchives task is used
                required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }

                // Sign JAR artifacts
                sign configurations.archives

                // Sign Maven POM
                beforeDeployment {
                    org.gradle.api.artifacts.maven.MavenDeployment deployment -> signing.signPom(deployment)
                }
            }
        }

        // Set login credentials for repository
        repository(url: sonatypeUrl) {
            authentication(userName: System.getenv("sonatypeUsername"), password: System.getenv("sonatypePassword"))
        }

        // Maven POM generation
        pom.project {
            name projectName
            artifactId archivesBaseName
            packaging packaging
            inceptionYear inceptionYear
            url url
            description project.ext.description

            scm {
                connection 'scm:git:git://github.com/InspireNXE/Pulse.git'
                developerConnection 'scm:git:ssh://git@github.com:InspireNXE/Pulse.git'
                url 'https://github.com/InspireNXE/Pulse'
            }

            licenses {
                license {
                    name 'The MIT License'
                    url 'http://www.tldrlegal.com/license/mit-license'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Zidane'
                    name 'Chris Sanders'
                    email 'zidane@outlook.com'
                }
                developer {
                    id 'Grinch'
                    name 'Steven Downer'
                    email 'grinch@outlook.com'
                }
            }

            organization {
                name organization
                url url
            }

            issueManagement {
                system 'github'
                url 'https://github.com/InspireNXE/Pulse/issues'
            }
        }
    }
}
