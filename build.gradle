// Default tasks
defaultTasks 'licenseFormatMain', 'licenseFormatTest', 'clean', 'build', 'shadowJar'

// Apply plugins
apply plugin: 'application'
apply plugin: 'cobertura'
apply plugin: 'coveralls'
apply plugin: 'java'
apply plugin: 'license'
apply plugin: 'maven'
apply plugin: 'shadow'
apply plugin: 'signing'

// Basic project information
group = 'org.inspirenxe'
archivesBaseName = 'pulse'
version = '1.0.0-SNAPSHOT'
mainClassName = "org.inspirenxe.server.Main"

// Extended project information
ext.projectName = 'Pulse'
ext.inceptionYear = '2014'
ext.packaging = 'jar'
ext.url = 'http://inspirenxe.org'
ext.description = 'Open source, multi-threaded Minecraft server written in Java.'
ext.organization = 'InspireNXE'
ext.buildNumber = project.hasProperty("buildNumber") ? buildNumber : '0'

// Minimum version of Java required
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// Configuration settings
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // Always check for new snapshots
}

// Plugin repositories and dependencies
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-nexus-public'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.2' // Coveralls plugin dependency
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.7.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.2.4'
    }
}

// Non-plugin repositories and dependencies
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'sonatype-nexus-public'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
}
dependencies {
    compile 'com.beust:jcommander:1.32'
    compile 'com.flowpowered:flow-chat-commands:0.1.0-SNAPSHOT'
    compile 'com.flowpowered:flow-commons:0.1.0-SNAPSHOT'
    compile 'com.flowpowered:flow-events:0.1.0-SNAPSHOT'
    compile 'com.flowpowered:flow-math:0.1.0-SNAPSHOT'
    compile 'com.flowpowered:flow-networking:0.1.0-SNAPSHOT'
    compile 'com.flowpowered:flow-noise:0.1.0-SNAPSHOT'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'jline:jline:2.11'
    compile 'junit:junit:4.11'
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
    testCompile 'junit:junit:4.11'
}

// Filter, process, and include resources
processResources {
    from(rootProject.rootDir) {
        include 'LICENSE.txt'
    }
}

// Include dependencies in final JAR
shadow {
    artifactAttached = false
    exclude 'junit/**'
}

// License header formatting
import nl.javadude.gradle.plugins.license.License
tasks.withType(License).each { licenseTask ->
    licenseTask.exclude 'resources/**'
    licenseTask.exclude '**/*.xml'
    licenseTask.exclude '**/*.json'
}
license {
    ext.name = projectName
    ext.organization = organization
    ext.url = url
    ext.year = inceptionYear
    header rootProject.file('HEADER.txt')
    ignoreFailures true
    strictCheck true
}

// Source compiler configuration
configure([compileJava, compileTestJava]) {
    options.compilerArgs << '-Xlint:all'
    options.compilerArgs << '-Xlint:-path'
    options.deprecation = true
}

// JAR manifest configuration
manifest.mainAttributes(
        "Main-Class": mainClassName,
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": version + ".b" + buildNumber,
        "Implementation-Vendor": url)

// Coveralls report configuration
cobertura.coverageFormats = [ 'html', 'xml' ] // Coveralls requires xml format

// Artifact deployment
uploadArchives {
    repositories.mavenDeployer {
        // Javadoc JAR generation
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from 'build/docs/javadoc'
        }

        // Source JAR generation
        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.java.srcDirs
        }

        // Set all artifacts
        artifacts {
            archives jar, javadocJar, sourcesJar
        }

        // Tasks and variables based on if release or snapshot
        if (version.endsWith('-SNAPSHOT')) {
            // Set variable to snapshots repository URL
            ext.sonatypeUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
        } else {
            // Set variable to releases repository URL
            ext.sonatypeUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

            // Deployment signing
            signing {
                // Check if uploadArchives task is used
                required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }

                // Sign JAR artifacts
                sign configurations.archives

                // Sign Maven POM
                beforeDeployment {
                    org.gradle.api.artifacts.maven.MavenDeployment deployment -> signing.signPom(deployment)
                }
            }
        }

        // Set login credentials for repository
        repository(url: sonatypeUrl) {
            authentication(userName: System.getenv("sonatypeUsername"), password: System.getenv("sonatypePassword"))
        }

        // Maven POM generation
        pom.project {
            name projectName
            artifactId archivesBaseName
            packaging packaging
            inceptionYear inceptionYear
            url url
            description project.ext.description

            scm {
                connection 'scm:git:git://github.com/InspireNXE/Pulse.git'
                developerConnection 'scm:git:ssh://git@github.com:InspireNXE/Pulse.git'
                url 'https://github.com/InspireNXE/Pulse'
            }

            licenses {
                license {
                    name 'The MIT License'
                    url 'http://www.tldrlegal.com/license/mit-license'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Zidane'
                    name 'Chris Sanders'
                    email 'zidane@outlook.com'
                }
                developer {
                    id 'Grinch'
                    name 'Steven Downer'
                    email 'grinch@outlook.com'
                }
            }

            organization {
                name organization
                url url
            }

            issueManagement {
                system 'github'
                url 'https://github.com/InspireNXE/Pulse/issues'
            }
        }
    }
}
